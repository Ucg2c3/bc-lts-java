import java.nio.file.CopyOption
import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardCopyOption
import static ExtraFunctions.*

plugins {
  id "biz.aQute.bnd.builder" version "7.0.0"
}

jar.archiveBaseName = "bctls-lts$vm_range"

sourceSets {
    main {
      java {
        srcDirs = ['src/main/java', 'src/main/jdk1.5']
      }
    }
    java9 {
        java {
            srcDirs = ['src/main/jdk1.9']
        }
    }
}

dependencies {
  implementation project(':prov')
  implementation project(':util')

  implementation files("$lts_prov")
  implementation files("$lts_util")
    implementation project(path: ':core')
    testImplementation project(':pkix')


  java9Implementation files("$lts_prov")
  java9Implementation files("$lts_util")
  java9Implementation files(sourceSets.main.output.classesDirs) {
     builtBy compileJava
  }
}

compileJava {
 options.release = 8
  dependsOn(':prov:jar')
  dependsOn(':util:jar')
}

compileJava9Java {
  options.release = 9

  options.compilerArgs += [
                    '--module-path', "$lts_prov:$lts_util"
  ]
  options.sourcepath = files(['src/main/jdk1.5', 'src/main/java', 'src/main/jdk1.9'])

  dependsOn(':prov:jar')
  dependsOn(':util:jar')
}

jar {
  from sourceSets.main.output
  into('META-INF/versions/9') {
    from sourceSets.java9.output
  }
  String v = "${rootProject.extensions.ext.bundle_version}"
      manifest.attributes('Multi-Release': 'true')
      manifest.attributes('Bundle-Name': 'bctls')
      manifest.attributes('Bundle-SymbolicName': 'bctls')
      manifest.attributes('Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8')
      manifest.attributes('Export-Package': "org.bouncycastle.{jsse|tls}.*;version=${v}")
      manifest.attributes('Import-Package': "java.*;resolution:=optional,javax.*;resolution:=optional,!org.bouncycastle.{jsse|tls}.*,org.bouncycastle.*;version=\"[${v},${maxVersion})\"")
      manifest.attributes('Bundle-Version': "${v}")
      manifest.attributes('Permissions': 'all-permissions')
      manifest.attributes('Codebase': '*')
      manifest.attributes('Application-Library-Allowable-Codebase': '*')
      manifest.attributes('Caller-Allowable-Codebase': '*')
      manifest.attributes('Trusted-Library': 'true')
}

task sourcesJar(type: Jar) {
  archiveBaseName = jar.archiveBaseName
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
  into('META-INF/versions/9') {
    from sourceSets.java9.allSource
  }
  dependsOn(':prov:jar')
  dependsOn(':util:jar')
}

javadoc {
  title = "Bouncy Castle Library (LTS Edition) ${version} TLS API"
  options.windowTitle = "Bouncy Castle Library (LTS Edition) ${version} TLS API"
  options.overview = "overview.html"
  dependsOn(':prov:jar')
  dependsOn(':util:jar')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveBaseName = jar.archiveBaseName
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

task('copyJars', type:Copy) {
  from new File(projectDir,"build/libs")
  into new File(rootDir,"../bc-lts-java-jars/${version}/")
}

checkstyleMain {
  dependsOn(':prov:jar')
  dependsOn(':util:jar')
}

compileTestJava {
  dependsOn(':prov:jar')
  dependsOn(':util:jar')
}
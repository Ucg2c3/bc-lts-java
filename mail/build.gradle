import static ExtraFunctions.*

plugins {
    id "biz.aQute.bnd.builder" version "7.0.0"
}

jar.archiveBaseName = "bcmail-lts${vm_range}"

sourceSets {
    java9 {
        java {
            srcDirs = ['src/main/jdk1.9']
        }
    }
}

dependencies {
    implementation project(':prov')
    implementation project(':util')
    implementation project(':pkix')

    implementation group: 'javax.mail', name: 'mail', version: '1.4'
    implementation files("$lts_prov")
    implementation files("$lts_util")
    implementation files("$lts_pkix")
    implementation project(path: ':core')

    java9Implementation project(':prov')
    java9Implementation project(':util')
    java9Implementation project(':pkix')
    java9Implementation files(sourceSets.main.output.classesDirs) {
        builtBy compileJava
    }
}

compileJava {
    options.release = 8
    dependsOn(':prov:jar')
    dependsOn(':util:jar')
    dependsOn(':pkix:jar')

}

compileJava9Java {
    options.release = 9

    options.compilerArgs += [
            '--module-path', "$lts_prov:$lts_util:$lts_pkix"
    ]
    options.sourcepath = files(['src/main/java', 'src/main/jdk1.9'])
}

jar {
    from sourceSets.main.output
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    String v = "${rootProject.extensions.ext.bundle_version}"
    manifest.attributes('Multi-Release': 'true')
    manifest.attributes('Bundle-Name': 'bcmail')
    manifest.attributes('Bundle-SymbolicName': 'bcmail')
    manifest.attributes('Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8')
    manifest.attributes('Export-Package': "org.bouncycastle.mail.*;version=${v}")
    manifest.attributes('Import-Package': "java.*;resolution:=optional,javax.*;resolution:=optional,!org.bouncycastle.mail.*,org.bouncycastle.*;version=\"[${v},${maxVersion})\"")
    manifest.attributes('Bundle-Version': "${v}")
    manifest.attributes('Permissions': 'all-permissions')
    manifest.attributes('Codebase': '*')
    manifest.attributes('Application-Library-Allowable-Codebase': '*')
    manifest.attributes('Caller-Allowable-Codebase': '*')
    manifest.attributes('Trusted-Library': 'true')
}

task sourcesJar(type: Jar) {
    archiveBaseName = jar.archiveBaseName
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    into('META-INF/versions/9') {
        from sourceSets.java9.allSource
    }
    dependsOn(':prov:jar')
    dependsOn(':util:jar')
    dependsOn(':pkix:jar')
}

javadoc {
    title = "Bouncy Castle Library (LTS Edition) ${version} JavaMail API"
    options.windowTitle = "Bouncy Castle Library (LTS Edition) ${version} JavaMail API"
    options.overview = "overview.html"
    dependsOn(':prov:jar')
    dependsOn(':util:jar')
    dependsOn(':pkix:jar')

}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = jar.archiveBaseName
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.bouncycastle'
            artifactId = "bcmail-lts$vm_range"
            from components.java
    
            artifact(javadocJar)
            artifact(sourcesJar)
        }
    
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

task('copyJars', type: Copy) {
    dependsOn("jar")
    dependsOn("sourcesJar")
    dependsOn("javadocJar")

    from new File(projectDir, "build/libs")
    into new File(rootDir, "../bc-lts-java-jars/${version}/")
}

checkstyleMain {
    dependsOn(':prov:jar')
    dependsOn(':util:jar')
    dependsOn(':pkix:jar')
}

compileTestJava {
    dependsOn(":prov:jar")
    dependsOn(":util:jar")
    dependsOn(":pkix:jar")
}
